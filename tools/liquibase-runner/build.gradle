description = 'Liquibase runner for privatlakarportalen'

apply plugin: 'application'

mainClassName = "liquibase.integration.commandline.Main"
applicationDefaultJvmArgs = ["-Dfile.encoding=utf8"]

startScripts << {
    // Get hold of the file name of the statistik-service jar file
    def privPersistenceDependency = configurations.runtime.filter { it.name.startsWith("privatlakarportal-persistence") }.singleFile.name
    // Insert our default script parameters into the script templates
    unixScript.text = unixScript.text.replaceAll(/"\$@"$/,
            "--driver=com.mysql.jdbc.Driver --classpath=lib/${privPersistenceDependency} --changeLogFile=changelog/changelog.xml \"\\\$@\"")
    windowsScript.text = windowsScript.text.replaceAll(/%CMD_LINE_ARGS%/,
            "--driver=com.mysql.jdbc.Driver --classpath=lib/${privPersistenceDependency} --changeLogFile=changelog/changelog.xml %CMD_LINE_ARGS%")
}

artifacts {
    archives distZip
}

configurations.runtime {
    transitive = false
}

dependencies {
    runtime project(":privatlakarportal-persistence")
    runtime "org.liquibase:liquibase-core:${liquibaseVersion}"
    runtime "mysql:mysql-connector-java:5.1.32"
}