description = 'Inera Privatlakaportal PU-Integration module'

ext.wsdlFile = file("src/main/resources/schemas/wsdl/residentmaster/interactions/LookupResidentForFullProfileInteraction/LookupResidentForFullProfileInteraction_1.0_RIVTABP21.wsdl")
ext.generatedJavaDir = file("generatedSource/java")

sourceSets.main.java {
    srcDir generatedJavaDir
}

clean.doLast { ant.delete(dir: generatedJavaDir.parent) }

configurations {
    wsdlgen
}

task wsdl2Java(type: JavaExec) {
    inputs.files wsdlFile
    outputs.dir generatedJavaDir
    classpath configurations.wsdlgen
    jvmArgs "-Djavax.xml.accessExternalSchema=all"
    main "org.apache.cxf.tools.wsdlto.WSDLToJava"
    args "-xjc-XhashCode", "-xjc-Xequals", "-xjc-XtoString", "-xjc-Xinheritance",
         "-p", "urn:riv:population:residentmaster:LookupResidentForFullProfileResponder:1=se.inera.population.residentmaster.v1.lookupresidentforfullprofile",
         "-p", "urn:riv:population:residentmaster:1=se.inera.population.residentmaster.v1",
         "-p", "urn:riv:itintegration:registry:1=se.inera.itintegration.v1",
         "-p", "urn:riv:population:residentmaster:LookupResidentForFullProfile:1:rivtabp21=se.inera.population.residentmaster.v1",
         "-validate", "-verbose",
         '-d', generatedJavaDir.path, wsdlFile
}

compileJava.dependsOn(wsdl2Java)

dependencies {
    compile project(":privatlakarportal-common")

    compile "org.apache.cxf:cxf-rt-frontend-jaxws:${cxfVersion}"
    compile "org.apache.cxf:cxf-rt-transports-http:${cxfVersion}"
    compile "org.jvnet.jaxb2_commons:jaxb2-basics:0.6.4"
    compile "org.springframework:spring-context:${springContextVersion}"
    compile "org.springframework:spring-context-support:${springContextVersion}"
    compile 'ch.qos.logback:logback-classic:1.0.13'
    compile 'com.google.guava:guava:18.0'
    compile 'net.sf.ehcache:ehcache:2.10.0'

    wsdlgen "org.apache.cxf:cxf-tools-wsdlto-frontend-jaxws:${cxfVersion}"
    wsdlgen "org.apache.cxf:cxf-tools-wsdlto-databinding-jaxb:${cxfVersion}"
    wsdlgen "org.jvnet.jaxb2_commons:jaxb2-basics:0.6.4"

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile "org.springframework:spring-test:${springContextVersion}"
    testCompile 'org.unitils:unitils-core:3.3'
}
