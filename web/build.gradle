import org.gradle.api.tasks.Exec

buildscript {
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:1.2.0'
        classpath 'com.moowork.gradle:gradle-grunt-plugin:0.10'
	classpath 'se.inera.intyg:gradle-fitnesse-plugin:1.0-SNAPSHOT'
    }
}

apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'com.moowork.grunt'

dependencies {
    compile project(":privatlakarportal-persistence")

    compile 'org.springframework:spring-webmvc:4.1.4.RELEASE'
    compile 'org.springframework.data:spring-data-jpa:1.7.1.RELEASE'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.1'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    compile 'org.hibernate:hibernate-entitymanager:4.3.7.Final'
    compile 'com.zaxxer:HikariCP-java6:2.3.5'
    compile 'org.slf4j:slf4j-api:1.7.10'
    compile 'ch.qos.logback:logback-classic:1.0.13'
    compile 'wsdl4j:wsdl4j:1.6.3'

    compile 'org.apache.cxf:cxf-rt-frontend-jaxws:3.0.4'
    compile 'org.apache.cxf:cxf-rt-transports-http:3.0.4'

    compile 'junit:junit:4.12'
    compile 'org.mockito:mockito-all:1.10.19'

    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
}

grunt {
    colors = true
}

node {
    version = '0.12.4'
    npmVersion = '2.10.1'
    download = true
}

war {
    webAppDirName = 'build/webapp'
}

task bowerInstall(type: NodeTask, dependsOn:npmInstall) {
    script = file('node_modules/bower/bin/bower')
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file('bower.json')
    outputs.files file('src/main/webapp/bower_components')
}

grunt_build.dependsOn bowerInstall
build.dependsOn grunt_build


gretty {
    httpPort = 8090
    contextPath = '/'
    logbackConfigFile = "${projectDir}/src/main/resources/logback.xml"
    jvmArgs = [
        '-Dspring.profiles.active=dev',
        '-Dprivatlakarportal.config.file=' + projectDir + '/src/main/resources/dev.properties'
     ]
}
