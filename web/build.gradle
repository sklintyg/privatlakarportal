apply plugin: 'war'
apply plugin: 'org.gretty'
apply plugin: 'nebula.grunt'
apply plugin: 'idea'

test {
    exclude '**/*IT*'
}

task restAssuredTest(type: Test) {
    outputs.upToDateWhen { false }
    systemProperty "integration.tests.baseUrl", System.properties['baseUrl'] ?: 'http://localhost:8060/'
    systemProperty "integration.tests.actuatorUrl", System.properties['actuatorUrl'] ?: "http://localhost:8160/"
    include '**/*IT*'
    useJUnitPlatform()
}

idea {
    module {
        excludeDirs += file('node_modules')
        excludeDirs += file('src/main/webapp/bower_components')
    }
}

grunt {
    colors = true
}

node {
    version = "${nodeJsVersion}"
    npmVersion = "${npmVersion}"
    download = true
    distBaseUrl = 'https://nodejs.org/dist/'
    workDir = file("${rootProject.projectDir}/.gradle/nodejs")
    npmWorkDir = file("${rootProject.projectDir}/.gradle/npm")
}

if (project.hasProperty('useMinifiedJavaScript')) {
    war {
        webAppDirName = 'build/webapp'
    }
    war.dependsOn grunt_build
} else if (project.hasProperty('useNoMinifiedJavaScriptNoKarma')) {
    war.dependsOn grunt_build_no_minify_no_karma
} else {
    war.dependsOn grunt_build_no_minify
}

war.dependsOn createVersionPropertyFile

grunt_build.dependsOn npmInstall
grunt_build_no_minify.dependsOn npmInstall
grunt_build_no_minify_no_karma.dependsOn npmInstall


gretty {
    def instance = System.properties['instance'] ?: '0'
    def useHttpPort = 8060 + (instance as Integer)
    def useInternalHttpPort = 8160 + (instance as Integer)
    def applicationDir = "${rootProject.projectDir}/devops/dev"

    servletContainer = 'tomcat9'
    contextPath = '/'

    integrationTestTask = null
    scanInterval = 0
    debugPort = 8860 + (instance as Integer)
    portPropertiesFileName = "gretty_ports_${instance}.properties"
    debugSuspend = false
    fastReload = true
    inplaceMode = 'hard'
    serverConfigFile = "tomcat-gretty.xml"

    jvmArgs = [
            "-Dcatalina.base=${buildDir}/catalina.base",
            "-Dspring.profiles.active=dev,caching-enabled",
            "-Dlogback.file=${applicationDir}/config/logback-dev.xml",
            "-Djava.awt.headless=true",
            "-Dfile.encoding=UTF-8",
            "-Dapplication.dir=${applicationDir}",
            "-Ddev.config.file=${applicationDir}/config/application-dev.properties",
            "-Ddev.http.port=${useHttpPort}",
            "-Ddev.http.port.internal=${useInternalHttpPort}"]
}

dependencies {
    compile project(":privatlakarportal-wc-integration")

    compile "se.inera.intyg.infra:dynamiclink:${infraVersion}"
    compile "se.inera.intyg.infra:monitoring:${infraVersion}"
    compile "se.inera.intyg.infra:pu-integration:${infraVersion}"
    compile "se.inera.intyg.infra:postnummerservice-integration:${infraVersion}"
    compile "se.inera.intyg.infra:security-filter:${infraVersion}"
    compile "se.inera.intyg.infra:security-common:${infraVersion}"
    compile "se.inera.intyg.infra:privatepractitioner:${infraVersion}"

    compile "se.riv.itintegration.monitoring:itintegration-monitoring-schemas:${rivtaItintegrationMonitoringSchemasVersion}"

    compile "se.inera.intyg.schemas:schemas-contract:${schemasContractVersion}"

    compile "org.apache.cxf:cxf-rt-features-logging:${cxfVersion}"
    compile "org.springframework:spring-webmvc"
    compile "org.springframework.ws:spring-ws-core"
    compile "org.springframework.security:spring-security-config"
    compile "org.springframework.security.extensions:spring-security-saml2-core:${springSecurityExtensionsVersion}"
    compile "org.springframework.security:spring-security-web"
    compile "org.springframework.session:spring-session-data-redis"
    compile "com.googlecode.json-simple:json-simple:${jsonSimpleVersion}"
    compile "wsdl4j:wsdl4j"
    compile "io.springfox:springfox-swagger2:${swaggerVersion}"
    compile "io.springfox:springfox-swagger-ui:${swaggerVersion}"
    compile("io.swagger:swagger-jaxrs:${swaggerJaxrsVersion}") {
        exclude(module: "jsr311-api")
        transitive = false
    }

    compile "mysql:mysql-connector-java"

    compileOnly "javax.servlet:javax.servlet-api"
    compileOnly "jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api"

    testCompile "org.hamcrest:hamcrest"
    testCompile "org.hamcrest:hamcrest-library"
    testCompile "org.assertj:assertj-core"
    testCompile "org.antlr:ST4:${stAntlr4Version}"

    testCompile "io.rest-assured:rest-assured"
    testCompile "io.rest-assured:json-schema-validator"

    gretty "se.inera.intyg.refdata:refdata:${refDataVersion}"
}
