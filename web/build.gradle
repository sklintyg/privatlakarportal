apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'com.moowork.grunt'
apply plugin: 'idea'

test {
    exclude '**/*IT*'
}

task restAssuredTest(type: Test) {
    outputs.upToDateWhen { false }
    systemProperty "integration.tests.baseUrl", System.properties['baseUrl'] ?: 'http://localhost:8090/'
    include '**/*IT*'
}

idea {
    module {
        excludeDirs += file('node_modules')
        excludeDirs += file('src/main/webapp/bower_components')
    }
}

grunt {
    colors = true
}

node {
    version = '6.6.0'
    download = true
    distBaseUrl = 'https://build-inera.nordicmedtest.se/node/'
}

if (project.hasProperty('privatlakarportal.useMinifiedJavaScript')) {
    war {
        webAppDirName = 'build/webapp'
    }
    war.dependsOn grunt_build
} else {
    war.dependsOn grunt_build_no_minify
}

war.dependsOn createVersionPropertyFile

task bowerInstall(type: NodeTask, dependsOn:npmInstall) {
    script = file('node_modules/bower/bin/bower')
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file('bower.json')
    outputs.files file('src/main/webapp/bower_components')
}

grunt_build.dependsOn bowerInstall
grunt_build_no_minify.dependsOn bowerInstall

gretty {
    contextPath = '/'
    logbackConfigFile = "${projectDir}/src/main/resources/logback.xml"
    integrationTestTask = null
    scanInterval = 0
    httpPort = 8090
    debugPort = 5010
    debugSuspend = false
    fastReload = true
    inplaceMode = 'hard'

    jvmArgs = ['-Dspring.profiles.active=dev,caching-enabled',
               '-Dprivatlakarportal.config.file=' + projectDir + '/src/main/resources/dev.properties',
               '-Dprivatlakarportal.mailresource.file=' + projectDir + '/src/main/resources/dev.mailresource.properties',
               '-Dcredentials.file=' + projectDir + '/src/main/resources/dev-credentials.properties',
               '-Dprivatlakarportal.resources.folder=/' + projectDir + '/../src/main/resources',
               '-XX:MaxPermSize=256M']
}

dependencies {
    compile project(":privatlakarportal-wc-integration")

    compile "se.inera.intyg.infra:dynamiclink:${infraVersion}"
    compile "se.inera.intyg.infra:pu-integration:${infraVersion}"
    compile "se.inera.intyg.infra:postnummerservice-integration:${infraVersion}"

    compile "se.riv.itintegration.monitoring:itintegration-monitoring-schemas:${rivtaItintegrationMonitoringSchemasVersion}"

    compile "se.inera.intyg.schemas:schemas-contract:${schemasContractVersion}"

    compile "org.springframework:spring-webmvc:${springContextVersion}"
    compile "org.springframework.ws:spring-ws-core:${springWsCoreVersion}"
    compile "org.springframework.security:spring-security-config:${springSecurityVersion}"
    compile "org.springframework.security.extensions:spring-security-saml2-core:${springSecurityExtensionsVersion}"
    compile "org.springframework.security:spring-security-web:${springSecurityVersion}"
    compile "com.googlecode.json-simple:json-simple:${jsonSimpleVersion}"
    compile "wsdl4j:wsdl4j:${wsdl4jVersion}"
    compile "io.springfox:springfox-swagger2:${swaggerVersion}"
    compile "io.springfox:springfox-swagger-ui:${swaggerVersion}"
    compile("io.swagger:swagger-jaxrs:${swaggerJaxRsVersion}") {
        transitive = false;
    }

    compileOnly "javax.servlet:javax.servlet-api:${servletApiVersion}"
    compileOnly "jstl:jstl:${jstlVersion}"

    testCompile "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testCompile "org.unitils:unitils-core:${unitilsVersion}"
    testCompile "org.antlr:ST4:${stringTemplateVersion}"
    testCompile "com.jayway.restassured:rest-assured:${restAssuredVersion}"
    testCompile "com.jayway.restassured:json-schema-validator:${restAssuredVersion}"
}
