plugins {
    id "io.spring.dependency-management" version "1.1.6" apply false
    id "org.gretty" version "4.1.5" apply false
    id "com.netflix.nebula.node" version "3.0.0" apply false
    id 'test-report-aggregation'
    id 'org.sonarqube' version '5.1.0.4882'
    id "org.owasp.dependencycheck" version "10.0.4"
    id 'jacoco'
}

ext {
    infraVersion = System.properties['infraVersion'] ?: '4.0.0-SNAPSHOT'

    ppSchemasVersion = "1.0.5.2"
    ppTermsSchemasVersion = "1.0.5.2"
    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.5.2"
    schemasContractVersion = "2.1.8.2"

    commonsIOVersion = "2.14.0"
    cxfVersion = "4.0.5"
    guavaVersion = "33.0.0-jre"
    jakartaJwsApiVersion = "3.0.0"
    jakartaMail = "2.0.1"
    jsonSimpleVersion = "1.1.1"
    picocliVersion = "4.7.5"
    springSecurityExtensionsVersion = "1.0.3.RELEASE"
    stAntlr4Version = "4.3.4"
    springDocVersion = "1.8.0"
    shedlockSpringVersion = "5.16.0"

    nodeJsVersion = "12.22.12"
    npmVersion = "6.14.16"

    springbootMavenBomVersion = "3.3.4"

    nvdMirror = 'https://vulnz-nvd-mirror-devtest.intyg.nordicmedtest.se'
}

allprojects {
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'idea'

    group = 'se.inera.privatlakarportal'
    version = System.properties['buildVersion'] ?: '4.0.0-SNAPSHOT'
    sourceCompatibility = 21
    targetCompatibility = 21

    dependencyManagement {
        imports { mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}" }
    }

    compileJava {
        options.encoding = "UTF-8"
        options.compilerArgs.add("-parameters")
    }
    compileJava.dependsOn processResources

    dependencies {
        implementation "ch.qos.logback:logback-classic"
        implementation "jakarta.annotation:jakarta.annotation-api"
        implementation "jakarta.jws:jakarta.jws-api:${jakartaJwsApiVersion}"
        implementation "jakarta.xml.ws:jakarta.xml.ws-api"

        runtimeOnly "com.sun.xml.messaging.saaj:saaj-impl"

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation "junit:junit"
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testImplementation "org.mockito:mockito-junit-jupiter"
        testImplementation "org.mockito:mockito-core"
        testImplementation "org.springframework:spring-test"

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
        testRuntimeOnly "org.junit.vintage:junit-vintage-engine"
    }

    test { useJUnitPlatform() }

    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases/' }
        mavenCentral {
            content {
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/**",
                        "**/integrationtest/**"
                ])
            }))
        }
    }
}

reporting {
    subprojects.collect { subproject -> subproject.name }.each { subprojectName ->
        dependencies { testReportAggregation project(":${subprojectName}") }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-statistik"
        property "sonar.projectName", "intyg-statistik"
        property "sonar.dependencyCheck.jsonReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.json" as String
        property "sonar.dependencyCheck.htmlReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.html" as String
    }
}

dependencyCheck {
    formats = List.of("HTML", "JSON")
    nvd.setDatafeedUrl(nvdMirror)
    analyzers.setAssemblyEnabled(false)
}