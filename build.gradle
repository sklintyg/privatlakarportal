plugins {
    id "se.inera.intyg.plugin.common" version "3.1.3" apply false
    id "org.gretty" version "3.0.1" apply false
    id "com.moowork.node" version "1.3.1" apply false
    id "io.spring.dependency-management" version "1.0.9.RELEASE" apply false
}

ext {
    infraVersion = System.properties['infraVersion'] ?: '3.15.0-SNAPSHOT'
    refDataVersion = System.properties['refDataVersion'] ?: '1.0-SNAPSHOT'

    ppSchemasVersion = "1.0.5"
    ppTermsSchemasVersion = "1.0.5"

    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.5"

    schemasContractVersion = "2.1.8"

    commonsIOVersion = "2.6"
    cxfVersion = "3.2.4"
    geronimoJmsVersion = "1.1.1"
    guavaVersion = "28.2-jre"
    hibernateVersion = "5.3.15.Final"
    jadiraUserTypeVersion = "6.0.1.GA"
    jaxb2Version = "1.11.1"
    jaxbXjcVersion = "2.3.2"
    jaxbCoreVersion = "2.3.0.1"
    jaxbImplVersion = "2.3.2"
    jaxbOsgiVersion ="2.3.2"
    jsonSimpleVersion = "1.1.1"
    springSecurityExtensionsVersion = "1.0.3.RELEASE"
    stringTemplateVersion = "4.3"
    swaggerVersion = "2.9.2"
    swaggerJaxrsVersion = "1.6.0"
    unitilsVersion = "3.4.6"
    wsdl4jVersion = "1.6.3"
    shedlockSpringVersion = "4.5.0"

    nodeJsVersion = "10.16.2"
    npmVersion = "6.10.3"

    springbootMavenBomVersion = "2.2.5.RELEASE"
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            url "https://nexus.drift.inera.se/repository/it-public/"
        }
        jcenter {
            content {
                // this repository contains everything BUT artifacts with group starting with "se.inera"
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    // Errorprone has no mechanism (yet) for excluding generated code. We therefore disable it for hsa-integration.
    ext.errorproneExclude = "privatlakarportal-hsa-integration"

    apply plugin: 'se.inera.intyg.plugin.common'
    apply plugin: 'maven'
    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        imports { mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}" }
    }

    dependencies {
        // Exclude and import of non-BOM version of Hibernate due to
        // bug in BOM-version 5.4.12.
        compile("org.hibernate:hibernate-core") {
            version {
                strictly "${hibernateVersion}"
            }
        }
        compile("org.hibernate:hibernate-hikaricp") {
            version {
                strictly "${hibernateVersion}"
            }
        }
        compile("org.hibernate:hibernate-entitymanager") {
            version {
                strictly "${hibernateVersion}"
            }
        }

        compile "javax.annotation:javax.annotation-api"
        compile "jakarta.xml.bind:jakarta.xml.bind-api"
        compile "jakarta.xml.ws:jakarta.xml.ws-api"
        compile "ch.qos.logback:logback-classic"
        compile "org.slf4j:slf4j-api"
        compile "com.sun.xml.messaging.saaj:saaj-impl"

        testCompile "se.inera.intyg.refdata:refdata:${refDataVersion}"
        testCompile "junit:junit:4.+"
        testCompile "org.mockito:mockito-core"
        testCompile "org.springframework:spring-test"
    }

    group = 'se.inera.privatlakarportal'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    sourceCompatibility = 11
    targetCompatibility = 11

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    uploadArchives.enabled = false

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://nexus.drift.inera.se/repository/maven-releases/") {
            authentication(userName: System.properties['ineraNexusUsername'], password: System.properties['ineraNexusPassword'])
        }
    }
}

task tagRelease(type: TagReleaseTask)

def environment = project.findProperty('env') ?: "local"
se.inera.intyg.IntygPluginKt.addProjectPropertiesFromFile(project, file("$rootDir/env-properties/${environment}/settings.properties"))
