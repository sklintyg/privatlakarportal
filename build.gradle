// Load environment specific settings, if the -Penv=xxx parameter is given on invocation
task loadConfiguration {
    def environment = null
    if (project.hasProperty('env')) {
        environment = project.getProperty('env')
    } else {
        environment = "local"
    }
    logger.quiet "Loading configuration for environment '$environment'."
    addProjectPropertiesFromFile(file("$rootDir/env-properties/${environment}/settings.properties"))
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd HH:mm:ss')
    return formattedDate
}

apply plugin: 'sonar-runner'

allprojects {

    group = 'se.inera.privatlakarportal'

    if (System.env.BUILD_VERSION && System.env.BUILD_NUMBER) {
        version = "$System.env.BUILD_VERSION-b$System.env.BUILD_NUMBER"
    } else {
        version = '1.0.0-SNAPSHOT'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases/" }
        maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/snapshots/" }
        maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/thirdparty/" }
        maven { url "https://repository.jboss.org/nexus/content/repositories/releases" }

    }
    buildscript {
        repositories {
            mavenLocal()
            maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/snapshots/" }
            jcenter()
        }
    }
    
    sonarRunner {
        sonarProperties {
            property "sonar.jacoco.reportPath", "$buildDir/jacoco/test.exec"
        }
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.projectName", projectName
        property "sonar.projectKey", projectId

        // Address of SonarQube server
        property "sonar.host.url", "https://build-inera.nordicmedtest.se/sonar"

        ext.sonarPassword = System.properties['sonarPassword']

        // Database config
        property "sonar.jdbc.url", "jdbc:mysql://localhost:3306/sonar"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", "inera"
        property "sonar.jdbc.password", sonarPassword
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    sourceCompatibility = "1.7"
    targetCompatibility = "1.7"

    ext {
        cxfVersion = '3.0.4'
        springContextVersion = "4.1.4.RELEASE"
        springSecurityVersion = "3.2.5.RELEASE"
        springSecurityExtensionsVersion = "1.0.1.RELEASE"
        liquibaseVersion = "3.3.1"

        setProperty("buildTime", getDate().toString())
        setProperty("buildNumber", System.env.BUILD_NUMBER ? System.env.BUILD_NUMBER : "SNAPSHOT")
        setProperty("buildVersion", System.env.BUILD_VERSION ? System.env.BUILD_VERSION : "1.0.0-SNAPSHOT")
    }
    
    jacoco {
        toolVersion = "0.7.1.201405082137"
    }
    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination "${buildDir}/jacocoHtml"
        }
    }
}

private void addProjectPropertiesFromFile(propfile) {
    if (propfile.exists()) {
        def props = new Properties();
        propfile.withInputStream { props.load(it) }
        allprojects { subproject ->
            props.each { key, value ->
                subproject.ext.setProperty(key, value.toString())
            }
        }
    } else {
        throw new GradleScriptException("File '${propfile.path}' does not exist.", null)
    }
}

configure (subprojects.findAll {
    [
        'privatlakarportal-liquibase-runner',
        'privatlakarportal-web'
    ].contains(it.name) }) {

    apply plugin: 'maven'

    configurations { deployerJars }

    dependencies { deployerJars "org.apache.maven.wagon:wagon-webdav:1.0-beta-2" }

    ext.nexusUsername = System.properties['nexusUsername']
    ext.nexusPassword = System.properties['nexusPassword']

    uploadArchives {
        repositories {
            mavenDeployer {
                configuration = configurations.deployerJars
                repository(url: "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
                snapshotRepository(url: "https://build-inera.nordicmedtest.se/nexus/content/repositories/snapshots") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
                uniqueVersion = false
            }
        }
    }
}
